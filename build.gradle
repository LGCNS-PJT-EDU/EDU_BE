plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    /* git-hooks & 포맷터*/
    id 'com.github.jakemarsden.git-hooks' version '0.0.2'
    id 'com.diffplug.spotless' version '6.25.0'
    /* 품질 & 마이그레이션 */
    id 'org.flywaydb.flyway' version '9.22.3'
    id 'jacoco'
    id 'org.sonarqube' version '6.2.0.5505'
}

group = 'com.education'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //Apache Kafka
    implementation 'org.springframework.kafka:spring-kafka:3.2.0'
    //Spring Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    //Spring Actuator
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    // Swagger UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    //OAuth
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    //Google OAuth
    implementation 'com.google.api-client:google-api-client:2.4.0'
    implementation 'com.google.oauth-client:google-oauth-client-jetty:1.34.1'
    implementation 'com.google.http-client:google-http-client-jackson2:1.43.3'
    // Redis + Cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    // Kakao id_token 검증용
    implementation 'com.auth0:java-jwt:4.4.0'
    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    // Flyway
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    // MySQL JDBC 드라이버
    implementation 'mysql:mysql-connector-java:8.0.33'
    // Spring retry
    implementation 'org.springframework.retry:spring-retry'
    //websocket
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.webjars:stomp-websocket:2.3.3'

    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // 테스트코드 수행할대 필요한 H2
    testImplementation 'com.h2database:h2'

}

jacoco {
    toolVersion = '0.8.13'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy(
            tasks.named('jacocoTestReport'),
            tasks.named('jacocoTestCoverageVerification')
    )
}

gitHooks {
    // where to install your hooks; you can leave this at the default '.githooks'
    hooksDirectory = file('.git/hooks')

    // load each script file as a String so the plugin writes its content
    hooks = [
            'prepare-commit-msg': file('githooks-src/prepare-commit-msg').text,
            'commit-msg'        : file('githooks-src/commit-msg').text
    ]
}

spotless {
    java {
        googleJavaFormat('1.17.0') // 버전은 최신으로 변경 가능
        target 'src/**/*.java'
    }
}

def sonarCoverageIncludes = [
        '**/diagnosis/service/**/*.java',
        '**/exam/service/**/*.java',
        '**/feedback/service/**/*.java',
        '**/interview/service/**/*.java',
        '**/auth/service/**/*.java',
        '**/recommend/service/**/*.java',
        '**/roadmap/service/**/*.java',
        '**/solution/service/**/*.java',
        '**/kafka/**/consumer/**/*.java',
        '**/kafka/**/producer/**/*.java'
]

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.projectKey', 'LGCNS-PJT-EDU_EDU_BE'
        property 'sonar.organization', 'lgcns-pjt-edu'
        property 'sonar.sourceEncoding', 'UTF-8'

        property 'sonar.sources', 'src/main/java'
        property 'sonar.tests', 'src/test/java'
        property 'sonar.coverage.inclusions', sonarCoverageIncludes.join(',')

        property 'sonar.java.coveragePlugin', 'jacoco'
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        property "sonar.java.binaries", "${buildDir}/classes/java/main"
        property "sonar.java.test.binaries", "${buildDir}/classes/java/test"
    }
}

def jacocoIncludePatterns = [
        '**/diagnosis/service/**/*.class',
        '**/exam/service/**/*.class',
        '**/feedback/service/**/*.class',
        '**/interview/service/**/*.class',
        '**/auth/service/**/*.class',
        '**/recommend/service/**/*.class',
        '**/roadmap/service/**/*.class',
        '**/solution/service/**/*.class',
        '**/kafka/**/consumer/**/*.class',
        '**/kafka/**/producer/**/*.class'
]

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        xml.destination file("$buildDir/reports/jacoco/test/jacocoTestReport.xml")
    }

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, include: jacocoIncludePatterns)
                })
        )
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    afterEvaluate {
        classDirectories.setFrom(
                files(classDirectories.files.collect {
                    fileTree(dir: it, include: jacocoIncludePatterns)
                })
        )
    }

    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.00
            }
        }
    }
}
